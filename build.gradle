buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "Modmuss50"
            url = "http://maven.modmuss50.me/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'ModsIOUpload:ModsIOUpload:+'
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.0.9"
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'net.cazzar.gradle.modsio'
apply plugin: 'maven'
apply plugin: 'maven-publish'

version = "2.4.4"
group= "the_fireplace.overlord"
archivesBaseName = "Overlord-1.12"

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

minecraft {
    version = "1.12-14.21.1.2413"
    runDir = "run"
    replace '${version}', project.version
    mappings = "snapshot_20170628"
	//useDepAts = true
	
	clientRunArgs += "-uuid=0b1ec5ad-cb2a-43b7-995d-889320eb2e5b -username=The_Fireplace"
}

dependencies {
	deobfCompile group: "mezz.jei", name: "jei_1.12", version: "4.7.4.81"
	deobfCompile "info.amerifrance.guideapi:Guide-API:1.12-2.1.4-56"
	deobfProvided("codechicken:NotEnoughItems:1.12-2.4.0.229:universal") {
        exclude module: "jei_1.12"
		//Old mappings
		exclude module: "CodeChickenLib"
    }
	//Manually do CCL so the game doesn't crash
	deobfCompile "codechicken:CodeChickenLib:1.12-3.1.1.288:universal"
	deobfCompile "net.industrial-craft:industrialcraft-2:2.8.4-ex112"
}

repositories{
    maven { //JEI, Natura, Mantle, TiCon
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven { //Guide API
		url "http://tehnut.info/maven/"
	}
	maven { //Chicken Bones
		url "http://chickenbones.net/maven/"
	}
	maven { //IC2, Forestry
		url = "http://maven.ic2.player.to/"
	}
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task showVersion {
    description "Displays the current version"
    group "help"
    
    compileJava.dependsOn showVersion
    deobfCompileDummyTask.dependsOn showVersion
    extractMcpMappings.dependsOn showVersion
  
    doLast {
        def versionFile = new File(buildDir, '.version')
    
        versionFile.getParentFile().mkdirs()
        versionFile.text = version
    }
    
    outputs.upToDateWhen { false }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives jar
    archives deobfJar
    archives sourceJar
}

curseforge {
  apiKey = project.hasProperty("curseForgeApiKey")? project.curseForgeApiKey : "empty"
  project {
    id = '251732'
    changelog = file('changelog.txt')
    releaseType = 'release'
    addGameVersion '1.12'
	addGameVersion 'Java 8'
    mainArtifact(jar) {
        displayName = jar.archiveName.replace(".jar", "")
        relations{
			requiredLibrary 'guide-api'
			optionalLibrary 'industrial-craft'
		}
    }
    addArtifact(sourceJar) {
        displayName = sourceJar.archiveName.replace(".jar", "")
        changelog = "*This is a file for mod developers. If you don't know what to do with it, don't use it!*"
    }
    addArtifact(deobfJar) {
        displayName = deobfJar.archiveName.replace(".jar", "")
        changelog = "*This is a file for mod developers. If you don't know what to do with it, don't use it!*"
    }
  }
}

modsIO {
    key = project.hasProperty("modsioApiKey")? project.modsioApiKey : "empty"
    project{
		artifact = new File(jar.destinationDir, archivesBaseName+"-"+version+".jar")
		modid = "1239"
		minecraft = "1.12"
		try{changelog = file('changelog.txt').text}catch(Exception e){changelog=""}
		tag = ""
		current = true
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty("local_maven")) {
                repository(url: "file://${local_maven}")
                pom {
                    groupId = project.group
                    version = project.version
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging "jar"
                        description "Overlord"
                        url "https://github.com/The-Fireplace/Overlord"
                        issueManagement {
                            system "github"
                            url "https://github.com/The-Fireplace/Overlord/issues"
                        }
                        licenses {
                            license {
                                name "License"
                                url "https://github.com/The-Fireplace/Overlord/blob/master/LICENSE"
                                distribution "repo"
                            }
                        }
                        developers {
                            developer {
                                id "fireplace"
                                name "The_Fireplace"
                                roles {
                                    role "owner"
                                    role "developer"
                                }
                            }
                        }
                        contributors {
                            contributor {
                                name "The_BrainStone"
                                roles {
                                    role "translator"
                                }
                            }
                            contributor {
                                name "Dracindo"
                                roles {
                                    role "translator"
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

