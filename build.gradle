//file:noinspection GroovyAssignabilityCheck
plugins {
    // Dependencies
    id 'fabric-loom' version '0.10-SNAPSHOT'
    // Mod Hosting Publish
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'com.modrinth.minotaur' version '1.1.0'
    // Maven Publish
    id 'maven-publish'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.30.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

repositories {
    maven { url 'https://maven.shedaniel.me/' }
    maven { url 'https://maven.terraformersmc.com/releases/' }
    flatDir { dirs 'lib' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modCompileOnly "dev.the-fireplace:Fireplace-Lib:${project.fireplacelib_version}:api"
    modRuntimeOnly "dev.the-fireplace:Fireplace-Lib:${project.fireplacelib_version}"
    annotationProcessor "dev.the-fireplace:Annotated-DI:${project.annotateddi_version}:processor"

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}"
    modRuntime "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release.set(8)
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

jar {
    from('LICENSE') {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

if (hasProperty('curseForgeApiKey')) {
    curseforge {
        apiKey = curseForgeApiKey
        project {
            id = '251732'
            changelog = file('changelog.txt')
            releaseType = project.release_type
            addGameVersion project.minecraft_version
            addGameVersion 'Java 8'
            addGameVersion 'Fabric'
            mainArtifact(new File(new File(buildDir, 'libs'), "$archivesBaseName-${version}.jar")) {
                displayName = "$archivesBaseName-$version"
                relations {
                    requiredDependency 'fireplace-lib'
                    optionalDependency 'cloth-config'
                    optionalDependency 'modmenu'
                }
            }
        }
        options {
            javaIntegration = false
            forgeGradleIntegration = false
        }
    }
}

import com.modrinth.minotaur.TaskModrinthUpload
if (hasProperty('modrinthApiKey')) {
    task publishModrinth(type: TaskModrinthUpload) {
        token = modrinthApiKey
        projectId = 'HPV4V5CV'
        versionNumber = version
        versionName = project.mod_version
        uploadFile = new File(new File(buildDir, 'libs'), "$archivesBaseName-${version}.jar")
        changelog = file('changelog.txt').getText()
        releaseType = project.release_type
        addGameVersion(project.minecraft_version as String)
        addLoader('fabric')
    }
}

if (hasProperty('nexusUsername')) {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    name = 'Overlord'
                    packaging = 'jar'
                    description = 'A Minecraft mod allowing you to raise and command an army of skeletons.'
                    url = 'https://www.curseforge.com/minecraft/mc-mods/overlord'
                    scm {
                        connection = "scm:svn:https://github.com/The-Fireplace-Minecraft-Mods/${project.github_slug}.git"
                        developerConnection = "scm:svn:https://github.com/The-Fireplace-Minecraft-Mods/${project.github_slug}.git"
                        url = "https://github.com/The-Fireplace-Minecraft-Mods/${project.github_slug}"
                    }

                    licenses {
                        license {
                            name = 'Mozilla Public License 2.0'
                            url = 'https://www.mozilla.org/en-US/MPL/2.0/'
                        }
                    }

                    developers {
                        developer {
                            id = 'the_fireplace'
                            name = 'The_Fireplace'
                            email = 'the.f1repl4ce@gmail.com'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = nexusUsername
                    password = nexusPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }
}
